import groovy.time.TimeCategory
import org.gradle.api.logging.LogLevel;
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'com.diffplug.spotless'
    id 'idea'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    // Configure string concat to always inline compile
    options.compilerArgs.add '-XDstringConcat=inline'
}

tasks.withType(Test).configureEach { testTask ->

    // Allow adding a JVM arg when running tests via a Gradle Property named 'gearHeads.testJvmArg'.
    // In .github/workflows/gradle.yml this is used to specify where to put JVM crash logs.
    if (project.hasProperty('gearHeads.testJvmArg')) {
        jvmArgs project.properties['gearHeads.testJvmArg']
    }

    // Display test results as tests run, and a summary at the end.
    // See https://stackoverflow.com/a/36130467/95725
    testLogging {
        // Set options for log level LIFECYCLE
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
        events = [TestLogEvent.FAILED, TestLogEvent.SKIPPED]

        // Set options for log level DEBUG and INFO
        debug {
            events = [TestLogEvent.STARTED,
                      TestLogEvent.FAILED,
                      TestLogEvent.PASSED,
                      TestLogEvent.SKIPPED,
                      TestLogEvent.STANDARD_ERROR,
                      TestLogEvent.STANDARD_OUT]
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat
    }

    afterSuite { TestDescriptor desc, TestResult result ->
        if (!desc.parent) {  // will match the outermost suite
            def summary = "${result.resultType}"
            def logLevel = LogLevel.WARN
            if (result.failedTestCount > 0) {
                summary = "‚ùó${summary}‚ùó"
            } else {
                summary = "üíö ${summary} üíö"
                logLevel = LogLevel.LIFECYCLE
            }
            def message = "Task :${testTask.project.name}:${testTask.name} results: ${summary} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} passed, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped) " +
                    "in ${TimeCategory.minus(new Date(result.endTime), new Date(result.startTime))}"
            logger.log(logLevel, message)
        }
    }
}

tasks.named('jar') {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
        )
    }
}

spotless {
    ratchetFrom 'origin/main' // Update license years for files changed since 'origin/main'.
    // See https://github.com/diffplug/spotless/tree/main/plugin-gradle
    format 'misc', {
        // define the files to apply `misc` to
        target '.gitattributes', '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    groovyGradle {
        greclipse().configFile('../greclipse-gradle.properties')
    }
    java {
        importOrder() // Use the default importOrder configuration (can override)
        removeUnusedImports()

        // Use Google Java Format
        // - aosp() causes it to use a four-space indent
        googleJavaFormat('1.24.0').reflowLongStrings()

        formatAnnotations()  // fix formatting of type annotations

        licenseHeaderFile("../java-copyright-header.txt")
    }
}

javadoc {
    options {
        // CTRE (Phoenix 5 & 6)
        links += "https://api.ctr-electronics.com/phoenix6/stable/java/"
        // WPILib
        links += "https://github.wpilib.org/allwpilib/docs/release/java/"
        // REVLib
        links += "https://codedocs.revrobotics.com/java/"
        // PhotonVision
        links += "https://javadocs.photonvision.org/release/"

        // Converts `@apiNote` paragraph to "Note: " section in the generated javadoc
        tags += 'apiNote:a:Api Note:'

        // Converts `@implSpec` paragraph to "Implementation Requirements: " section in the generated javadoc
        tags += 'implSpec:a:Implementation Requirements:'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
