plugins {
    id 'java-common-conventions'
    id 'edu.wpi.first.GradleRIO' version '2026.1.1'
    id 'maven-publish'
}

version = '2.0.0-rc-1'

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()
    implementation project(':lib')

    testImplementation(platform('org.junit:junit-bom:5.13.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation 'com.google.truth:truth:1.4.4'
    testImplementation project(':testing')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    testRuntimeOnly('org.junit.vintage:junit-vintage-engine')

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()
}

wpi.java.configureTestTasks(test)

test {
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

publishing {
    publications {
        lib2813(MavenPublication) {
            groupId = 'com.team2813.lib2813'
            artifactId = 'vision'
            from components.java
            artifact sourceJar
            pom {
                name = '2813 Vision Library'
                description = 'Utilities for location and vision for FRC robots'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'cuttestkittensrule'
                        name = 'Kyle'
                        email = 'mangoiscute95@gmail.com'
                    }
                    developer {
                        id = 'kcooney'
                        name = 'Kevin'
                        email = 'kcooney@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Prospect-Robotics/lib2813.git'
                    developerConnection = 'scm:git:git://github.com/Prospect-Robotics/lib2813.git'
                    url = 'https://github.com/Prospect-Robotics/lib2813'
                }
                organization {
                    name = 'FRC 2813 Gear Heads'
                    url = 'https://team2813.com'
                }
            }
        }
    }
}
