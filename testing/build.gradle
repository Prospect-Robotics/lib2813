plugins {
    id 'java-common-conventions'
    id 'edu.wpi.first.GradleRIO' version '2026.1.1-beta-1'
    id 'maven-publish'
}

version = '2.0.0'

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()
    implementation 'com.google.truth:truth:1.4.4'
    implementation(platform('org.junit:junit-bom:5.13.1'))
    implementation('org.junit.jupiter:junit-jupiter')
    implementation project(':lib')
    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    testImplementation('org.junit.platform:junit-platform-testkit')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

// the magic line that makes tests work :)
wpi.java.configureTestTasks(test)

tasks.named('test') {
    // Support running both JUnit Vintage and JUnit Jupiter tests
    useJUnitPlatform {
        excludeTags('ignore-outside-testkit')
    }
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

publishing {
    publications {
        lib2813(MavenPublication) {
            groupId = 'com.team2813.lib2813'
            artifactId = 'testing'
            from components.java
            artifact sourceJar
            pom {
                name = '2813 Testing Library'
                description = 'Utilities for testing FRC robots'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mango'
                        name = 'Kyle'
                        email = 'mangoiscute95@gmail.com'
                    }
                    developer {
                        id = 'kcooney'
                        name = 'Kevin'
                        email = 'kcooney@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Prospect-Robotics/lib2813.git'
                    developerConnection = 'scm:git:git://github.com/Prospect-Robotics/lib2813.git'
                    url = 'https://team2813.com'
                }
                organization {
                    name = '2813 Gear Heads'
                    url = 'https://team2813.com'
                }
            }
        }
    }
}
